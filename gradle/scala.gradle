apply plugin: 'scala'

// Specify properties
ext {
  scalaBaseVersion = '2.10'
  scalaVersion = hasProperty('scalaVersion') ? getProperty('scalaVersion') : '2.10.4'
}

// Activate Zinc compiler and configure scalac
tasks.withType(ScalaCompile) {
    scalaCompileOptions.useCompileDaemon = false
    scalaCompileOptions.useAnt = false
    scalaCompileOptions.additionalParameters = ['-target:jvm-1.6']
}

// Rename all jars to a new name
tasks.withType(Jar) {
    baseName = "${project.name}_${scalaBaseVersion}"
}

// Create jar
task testJar(type: Jar, dependsOn: testClasses) {
    appendix = 'test'
    from sourceSets.test.output
}

// Create jar with scaladoc
task scaladocJar(type: Jar) {
    classifier = 'scaladoc'
    from scaladoc
}

// Create a configuration containing only for test artifacts
configurations {
    testArchives
}

// Explicitly
artifacts {
    testArchives testJar
}

// If the plugin is in the context then we need to change the artifact id
plugins.withType(org.gradle.api.publish.maven.plugins.MavenPublishPlugin) {
    artifacts {
        toPublish scaladocJar
    }
    publishing.publications.all { p ->
        p.artifactId = "${project.name}_${scalaBaseVersion}"
    }
}

// Enable support for multi-node testing
apply from: "$rootDir/gradle/multiNodeTesting.gradle"

